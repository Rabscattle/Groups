plugins {
    id 'java'
    id 'com.github.johnrengelman.shadow' version '7.1.2'
    id 'com.github.sherter.google-java-format' version '0.9'
}

group = 'com.github.domcoon'
version = '1.0-SNAPSHOT'

repositories {
    mavenCentral()
    maven {
        name = 'spigotmc-repo'
        url = 'https://hub.spigotmc.org/nexus/content/repositories/snapshots/'
    }
    maven {
        name = 'sonatype'
        url = 'https://oss.sonatype.org/content/groups/public/'
    }

    // Command Framework
    maven { url = "https://repo.aikar.co/content/groups/aikar/" }
    // Scoreboard
    maven { url = "https://nexus-repo.jordanosterberg.com/repository/maven-releases/" }
}

dependencies {
    compileOnly 'org.spigotmc:spigot-api:1.19-R0.1-SNAPSHOT'

    // Commands
    implementation "co.aikar:acf-paper:0.5.1-SNAPSHOT"

    // Storage
    implementation 'com.zaxxer:HikariCP:5.0.1'
    implementation group: 'mysql', name: 'mysql-connector-java', version: '8.0.30'
    implementation 'org.mariadb.jdbc:mariadb-java-client:2.7.2'

    // Scoreboard (by Jordan Osterberg)
    implementation 'dev.jcsoftware:JScoreboards:2.1.5-RELEASE'

    // Testing
    testImplementation 'org.junit.jupiter:junit-jupiter:5.9.0'
}

test {
    useJUnitPlatform()
}

def targetJavaVersion = 17
java {
    def javaVersion = JavaVersion.toVersion(targetJavaVersion)
    sourceCompatibility = javaVersion
    targetCompatibility = javaVersion
    if (JavaVersion.current() < javaVersion) {
        toolchain.languageVersion = JavaLanguageVersion.of(targetJavaVersion)
    }
}

tasks.withType(JavaCompile).configureEach {
    if (targetJavaVersion >= 10 || JavaVersion.current().isJava10Compatible()) {
        options.release = targetJavaVersion
    }
}

processResources {
    def props = [version: version]
    inputs.properties props
    filteringCharset 'UTF-8'
    filesMatching('plugin.yml') {
        expand props
    }
}


